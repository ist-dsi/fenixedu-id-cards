/* GENERATE-IDENTIFICATION-CARDS.DML
 * Domain Model of the Generation Identification Card module
 * 
 */

/*
 * The Identification Card domain classes are all in the
 * org.fenixedu.idcards.domain package or some sub-package of
 * this package.
 * 
 */
package org.fenixedu.idcards.domain;

/*
 * Enums definitions
 */
 
enum org.fenixedu.idcards.domain.SantanderCardState as SantanderCardState;
enum org.fenixedu.idcards.domain.PickupLocation as PickupLocation;

/*
 *
 * Class definitions
 *
 */

class SantanderEntry {
	DateTime lastUpdate;
    String requestLine;
    String responseLine;    
    String errorDescription;
    String requestReason;
    Boolean wasExpiringNotified;
    Boolean wasPickupNotified;
}

class SantanderCardInfo {	
	String mifareNumber;
	String serialNumber;
	DateTime expiryDate;
	String cardName;
	String role;
	String identificationNumber;
	bytearray photo;
	PickupLocation pickupLocation;
}

class SantanderCardStateTransition {
	SantanderCardState state;
	DateTime transitionDate;
}

class SantanderUserInfo {
	String cardName;
}

class RaspberryPiSession {
	String ipAddress;
	String userMifare;
	DateTime createdAt;
}

/*
 * Relations definitions
 * 
 */

relation BennuSantanderEntry {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu;
	SantanderEntry playsRole santanderEntries {
		multiplicity *;
	}
}

relation BennuRaspberryPiSession {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu;
	RaspberryPiSession playsRole raspberryPiSession {
		multiplicity *;
	}
}

relation AdminRaspberryPiSession {
	.org.fenixedu.bennu.core.domain.User playsRole admin;
	RaspberryPiSession playsRole raspberryPiSession;
}

relation CardInfoRaspberryPiSession {
	SantanderCardInfo playsRole userCardInfo;
	RaspberryPiSession playsRole raspberryPiSession;
}

relation BennuSantanderCardInfo {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu;
	SantanderCardInfo playsRole santanderCardInfo {
		multiplicity *;
	}
}

relation UserSantanderEntry {
	.org.fenixedu.bennu.core.domain.User playsRole user;
	SantanderEntry playsRole currentSantanderEntry;
}

relation PreviousSantanderEntry {
   SantanderEntry playsRole previous;
   SantanderEntry playsRole next;
}

relation SantanderCardSantanderEntry {
	SantanderEntry playsRole santanderEntry;
	SantanderCardInfo playsRole SantanderCardInfo {
		multiplicity 0..1;
	}
}

relation SantanderCardTransitions {
	SantanderCardInfo playsRole santanderCard;
	SantanderCardStateTransition playsRole santanderCardStateTransitions {
		multiplicity 0..*;
	}
}

relation UserSantanderInfo {
	.org.fenixedu.bennu.core.domain.User playsRole user;
	SantanderUserInfo playsRole santanderUserInfo;
}
